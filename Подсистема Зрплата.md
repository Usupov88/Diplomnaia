### Зарплата

Все добавляемые объекты включаем в новую подсистему **Зарплата**, выведенную в командный интерфейс, и включаем в состав функциональной опции **ВестиРасчетЗарплаты**.

1. Добавить регистр сведений **Календарь**:
  * измерение — День (Дата);
  * ресурс — Рабочий (Число).
  
![Календарь](%D0%9A%D0%B0%D0%BB%D0%B5%D0%BD%D0%B4%D0%B0%D1%80%D1%8C.png)

2. Добавить план видов расчёта **Начисления**:
  * без кода и с наименованием разумной длины (например, 100);
  * на закладке «Расчёт» укажите:
    * использование периода действие,
    * зависимость от базы по периоду действия по тому же плану видов расчёта;
  
  ![Начисление](%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5.png)

  * создайте предопределенные виды расчёта:
    * **Больничный**, **Отпуск**, **ФиксированнаяПремия**;
    * **ОплатаПоОкладу** с вытесняющими видами расчета **Больничный** и **Отпуск**;
    * **ПремияПроцентом** с базовым и ведущим видом расчёта **ОплатаПоОкладу**.
![Оплата по окладу](%D0%9E%D0%BF%D0%BB%D0%B0%D1%82%D0%B0%20%D0%BF%D0%BE%20%D0%BE%D0%BA%D0%BB%D0%B0%D0%B4%D1%83.png)

![Премия процентом](%D0%9F%D1%80%D0%B5%D0%BC%D0%B8%D1%8F%20%D0%BF%D1%80%D0%BE%D1%86%D0%B5%D0%BD%D1%82%D0%BE%D0%BC.png)

1. Добавить регистр расчёта **Зарплата**:
  * с учётом по плану видов расчета **Начисления**;
  * использующий период действия и базовый период;
  * использующий в качестве графика регистр **Календарь**, со значением графика **Рабочий**, а датой графика — **День**;
  * с периодичностью «Месяц»;
  * с измерением **Сотрудник** (СправочникСсылка.Сотрудники) и ресурсом **Сумма** (ОпределяемыйТип.Сумма).
  
![Зарплата](%D0%97%D0%B0%D1%80%D0%BF%D0%BB%D0%B0%D1%82%D0%B0.png)

1. Добавить документ **НачислениеСписком** для начисления фиксированных сумм по списку сотрудников:
  * с обязательным реквизитом **Начисление** (ВидРасчетаСсылка.Начисления);
  * с обязательными реквизитами **ПериодДействияНачало** и **ПериодДействияКонец** (Дата и время);
  * с табличной частью **Сотрудники** с реквизитами **Сотрудник** (СправочникСсылка.Сотрудники) и **Сумма** (ОпределяемыйТип.Сумма);
  * 
![Начисление списком](%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5%20%D1%81%D0%BF%D0%B8%D1%81%D0%BA%D0%BE%D0%BC.png)

  * на закладке «Движения» сделать его регистратором для регистра расчета **Зарплата** и для регистра бухгалтерии **Управленческий**;
  
  ![Регистратор начисления](%D0%A0%D0%B5%D0%B3%D0%B8%D1%81%D1%82%D1%80%D1%82%D0%BE%D1%80%20%D0%BD%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F.png)

  * в обработке проведения для каждой строки формировать движения:
    * по регистру расчёта **Зарплата**, не забыв заполнить вид расчёта (по реквизиту **Начисление**), период регистрации (дата документа) и период действия (согласно реквизитам **ПериодДействияНачало** и **ПериодДействияКонец**). Период действия понадобится в дальнейшем для вытеснения оплаты по окладу больничными и отпусками, введенными этим документом;
    * по регистру бухгалтерии **Управленческий** в Дт счета **Расходы** и в Кт счета **РасчетыССотрудниками** с заполнением субконто **Сотрудник** на сумму начисления.

  Процедура ОбработкаПроведения(Отказ, Режим)  
	
	//Создаем Новую запись
	Движения.Зарплата.Очистить();
	Движения.Зарплата.Записывать=Истина; 
	
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записывать = Истина;
	
	Для Каждого ТекСтрока Из Сотрудники Цикл
		
		Движение = Движения.Зарплата.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Начисление;
		Движение.ПериодДействияНачало = НачалоПериодаДействия;
		Движение.ПериодДействияКонец = КонецПериодаДействия;
		Движение.ПериодРегистрации = Дата;
		Движение.Сотрудник = ТекСтрока.Сотрудник;
		Движение.Сумма = ТекСтрока.Сумма;
		
		Движение = Движения.Управленческий.Добавить(); 
		Движение.Период = Дата;
		Движение.СчетДт = ПланыСчетов.Управленческий.Расходы;
		Движение.СчетКт = ПланыСчетов.Управленческий.РассчетыССотрудниками;
		Движение.СубконтоКт [ ПланыВидовХарактеристик.ВидыСубконто.Сотрудники ] = ТекСтрока.Сотрудник;
		Движение.Сумма = ТекСтрока.Сумма;
	КонецЦикла;
	Движения.Зарплата.Записать();
	Движения.Управленческий.Записать();
		  
КонецПроцедуры 

1. Добавить документ **НачислениеОплатыПоОкладу** для начисления оплаты по окладу с учётом фактически отработанного времени:
  * с обязательным реквизитом **ЗаМесяц** типа Дата с форматом «MM.yyyy», чтобы пользователь видел только месяц;
  * с табличной частью **Сотрудники** с единственным реквизитом **Сотрудник** (СправочникСсылка.Сотрудники);
  
![Начисление по оклду](%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5%20%D0%BF%D0%BE%20%D0%BE%D0%BA%D0%BB%D0%B0%D0%B4%D1%83.png)

  * в обработке проведения:
    * записать движения, пока без сумм, по регистру расчёта **Зарплата**. При этом:
      * в качестве периода регистрации указать дату документа, а в качестве периода действия — начало и конец месяца даты **ЗаМесяц**;
      * в качестве вида расчёта указать предопределенный ПланыВидовРасчета.Начисления.ОплатаПоОкладу;
    * после записи движений документа, выполнить запрос к ним, в запросе:
      * получить оклад для каждого сотрудника,
      * получить факт и норму из соответствующей виртуальной таблицы регистра расчёта,
      * получить номер строки движений для последующего обращения к нужному;
    * обойти результат запроса, на каждой итерации цикла выполнив расчёт суммы путем умножения оклада на частное деления факта на норму:
  * определив сумму, сформировать парное движение на такую же сумму по регистру бухгалтерии **Управленческий** в Дт счета **Расходы** и в Кт счета **РасчетыССотрудникми**, заполнив субконто **Сотрудник**.
   * создав документ, заполните календарь и убедитесь в том, что документ:
     * в отсутствие вытесняющих начислений начисляет за месяц полную сумму оклада,
     * после ввода оклада или больничного документом **НачислениеСписком** уменьшает начисления по окладу пропорционально дням невыхода.
  
Процедура ОбработкаПроведения(Отказ, Режим)
	
	
	//Новая запись 
	Движения.Зарплата.Очистить();
	Движения.Зарплата.Записывать = Истина;
	
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НачислениеОплатыПоОкладуСотрудники.Сотрудники КАК Сотрудники,
	|	НачислениеОплатыПоОкладуСотрудники.Сотрудники.Оклад КАК Оклад
	|ИЗ
	|	Документ.НачислениеОплатыПоОкладу.Сотрудники КАК НачислениеОплатыПоОкладуСотрудники
	|ГДЕ
	|	НачислениеОплатыПоОкладуСотрудники.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка",Ссылка);
	
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для  Каждого Строка Из Результат Цикл
		
		//Движение по регистру расчета Зарплата
		Движение = Движения.Зарплата.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ПланыВидовРасчета.Начисления.ОплатаПоОкладу;
		Движение.ПериодРегистрации = Дата;
		Движение.ПериодДействияНачало = НачалоМесяца(ЗаМесяц);
		Движение.ПериодДействияКонец = КонецМесяца(ЗаМесяц);
		Движение.Сотрудник = Строка.Сотрудники;
		Движение.Показатель = Строка.Оклад; 
		
		// Движение по регистру бухгалтерии Управленческий
		Проводник = Движения.Управленческий.Добавить();
		Проводник.СчетДт = ПланыСчетов.Управленческий.Расходы;
		Проводник.СчетКт = ПланыСчетов.Управленческий.РассчетыССотрудниками;
		Проводник.СубконтоКт [ ПланыВидовХарактеристик.ВидыСубконто.Сотрудники ] = Строка.Сотрудники;
		Проводник.Период = Дата; 
		
	КонецЦикла;
	
	Движения.Зарплата.Записать();
	Движения.Управленческий.Записать();
	
	РассчетНачисленийПоОкладу();
КонецПроцедуры
 
 Процедура РассчетНачисленийПоОкладу()
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗарплатаДанныеГрафика.РабочийПериодДействия КАК Норма,
	|	ЗарплатаДанныеГрафика.РабочийФактическийПериодДействия КАК Факт,
	|	ЗарплатаДанныеГрафика.НомерСтроки КАК НомерСтроки,
	|	ЗарплатаДанныеГрафика.Показатель КАК Показатель
	|ИЗ
	|	РегистрРасчета.Зарплата.ДанныеГрафика(
	|			Регистратор = &Регистратор
	|				И ВидРасчета = &Оклад) КАК ЗарплатаДанныеГрафика";
	
	Запрос.УстановитьПараметр("Регистратор",Ссылка);
	Запрос.УстановитьПараметр("Оклад",ПланыВидовРасчета.Начисления.ОплатаПоОкладу);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока  Выборка.Следующий() Цикл
		
		Движение = Движения.Зарплата[Выборка.НомерСтроки - 1];
		Движение.Сумма = Выборка.Показатель * Выборка.Факт / Выборка.Норма;
		
		Движение = Движения.Управленческий[Выборка.НомерСтроки - 1];
		Движение.Сумма = Выборка.Показатель * Выборка.Факт / Выборка.Норма; 
		
	КонецЦикла;	
	
	Движения.Зарплата.Записать(, Истина); 
	Движения.Управленческий.Записать();
КонецПроцедуры 

 
   

1. Добавить документ **НачислениеПремииПроцентом** для начисления премии процентом от оклада:
  * с обязательным числовым реквизитом **Процент**;
  * с обязательным реквизитом **ЗаМесяц** типа Дата с форматом «MM.yyyy», чтобы пользователь видел только месяц;
  * с табличной частью **Сотрудники** с единственным реквизитом **Сотрудник** (СправочникСсылка.Сотрудники);
![Начисление премии процентом](%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5%20%D0%BF%D1%80%D0%B5%D0%BC%D0%B8%D0%B8%20%D0%BF%D1%80%D0%BE%D1%86%D0%B5%D0%BD%D1%82%D0%BE%D0%BC.png)

  * в обработке проведения:
    * записать движения, пока без сумм, по регистру расчета **Зарплата**. При этом:
      * в качестве периода регистрации указать дату документа, а в качестве периода действия и базового периода действия — начало и конец месяца даты **ЗаМесяц**;
      * в качестве вида расчета указать предопределенный ПланыВидовРасчета.Начисления.ПремияПроцентом;
    * записав движения, получить базу запросом к соответствующей виртуальной таблице;
    * обойти результат запроса, на каждой итерации цикла выполнив расчёт премии, умножив базу на процент премии и поделив на 100.
  * определив сумму, сформировать парное движение на такую же сумму по регистру бухгалтерии **Управленческий** в Дт счета **Расходы** и в Кт счета **РасчетыССотрудниками**, заполнив субконто **Сотрудник**;
  * создав документ, проверьте себя, начислив премию процентом от оклада и выплатив все начисления документом **СписаниеДенежныхСредств**. В оборотно-сальдовой ведомости кредитовые обороты счета **РасчетыССотрудниками** должны полностью закрыться дебетовыми, и остаток стать нулевым.

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	//НоваяЗапись
	Движения.Зарплата.Очистить();
	Движения.Зарплата.Записывать = Истина; 
	
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записывать = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	НачислениеПремииПроцентомСотрудники.Сотрудник КАК Сотрудник,
	|	НачислениеПремииПроцентомСотрудники.Сотрудник.Оклад КАК Оклад
	|ИЗ
	|	Документ.НачислениеПремииПроцентом.Сотрудники КАК НачислениеПремииПроцентомСотрудники
	|ГДЕ
	|	НачислениеПремииПроцентомСотрудники.Ссылка = &Ссылка"; 
	
	Запрос.УстановитьПараметр( "Ссылка",Ссылка);
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Для каждого Строка из Выборка Цикл
		
		//Движение по регистру расчета Зарплата
		Движение = Движения.Зарплата.Добавить();
		Движение.ВидРасчета = ПланыВидовРасчета.Начисления.ПремияПроцентом; 
		Движение.Сторно = Ложь;
		Движение.ПериодДействияНачало = НачалоМесяца(ЗаМесяц);
		Движение.ПериодДействияКонец = КонецМесяца(ЗаМесяц);
		Движение.БазовыйПериодНачало = НачалоМесяца(ЗаМесяц); 
		Движение.БазовыйПериодКонец = КонецМесяца(ЗаМесяц);
		Движение.ПериодРегистрации = Дата; 
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Показатель = Строка.Оклад; 
		
		//Движение по регистру бухгалтерии Управленческий
		Проводник = Движения.Управленческий.Добавить();
		Проводник.СчетДт = ПланыСчетов.Управленческий.Расходы;
		Проводник.СчетКт = ПланыСчетов.Управленческий.РассчетыССотрудниками;
		Проводник.СубконтоКт [ ПланыВидовХарактеристик.ВидыСубконто.Сотрудники ] = Строка.Сотрудник;
		Проводник.Период = Дата;
		
	КонецЦикла;
	Движения.Зарплата.Записать();
	Движения.Управленческий.Записать();
	
	РассчитатьПремиюПоПроценту(); 
	
КонецПроцедуры

Процедура 	РассчитатьПремиюПоПроценту()

	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	ЗарплатаБазаЗарплата.НомерСтроки КАК НомерСтроки,
	                |	ЗарплатаБазаЗарплата.СуммаБаза КАК СуммаБаза
	                |ИЗ
	                |	РегистрРасчета.Зарплата.БазаЗарплата(
	                |			&Измерения,
	                |			&Измерения,
	                |			,
	                |			Регистратор = &Регистратор
	                |				И ВидРасчета = &ВидРасчета) КАК ЗарплатаБазаЗарплата";
	
	Запрос.УстановитьПараметр("Регистратор",Ссылка);
	Запрос.УстановитьПараметр("ВидРасчета",ПланыВидовРасчета.Начисления.ПремияПроцентом);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерения",Измерения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Движение = Движения.Зарплата [Выборка.НомерСтроки - 1 ];
		Движение.Сумма = Выборка.СуммаБаза * Процент / 100;
		
		Проводник = Движения.Управленческий [Выборка.НомерСтроки - 1];
		Проводник.Сумма = Выборка.СуммаБаза * Процент / 100;   
		
	КонецЦикла;
	
	Движения.Зарплата.Записать(,Истина);
	Движения.Управленческий.Записать();
	
КонецПроцедуры